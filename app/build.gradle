def buildAsLibrary = project.hasProperty('BUILD_AS_LIBRARY')
def buildAsApplication = !buildAsLibrary
if (buildAsApplication) {
    apply plugin: 'com.android.application'
}
else {
    apply plugin: 'com.android.library'
}

android {

    compileSdkVersion 32
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 3
        versionName "0.9.28.3"
        externalNativeBuild {
            ndkBuild {
                arguments "APP_PLATFORM=android-21"
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "store"
    productFlavors {
        playStore {
            if (buildAsApplication) {
                dimension "store"
                applicationId "org.tuxpaint.android"
            }
        }
        offPlayStore {
            if (buildAsApplication) {
                dimension "store"
                applicationId "org.tuxpaint"
            }
        }
    }
    androidResources {
        ignoreAssetsPattern 'nonexisting.file'
    }
    lint {
        abortOnError false
    }
    if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
        sourceSets.main {
            jniLibs.srcDir 'libs'
        }
        externalNativeBuild {
            ndkBuild {
                path 'src/main/jni/Android.mk'
            }
        }
       
    }

    if (buildAsLibrary) {
        libraryVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith(".aar")) {
                    def fileName = "org.libsdl.app.aar"
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.print:print:1.0.0"
}
